plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

// Set project version using a property, defaulting to 'unspecified' if not found
version = project.properties[project.name + '.version'] ?: 'undefined'
group = 'io.github.mcengine'

ext {
    // User
    user_git_name = System.getenv('USER_GIT_NAME') ?: 'null'

    // GitHub
    user_github_token = System.getenv('USER_GITHUB_TOKEN') ?: ''

    // Organization
    organization_name = System.getenv('ORGANIZATION_NAME') ?: 'MCEngine'
    organization_token = System.getenv('ORGANIZATION_TOKEN') ?: 'null'

    // Repository
    repository_name = System.getenv('REPOSITORY_NAME') ?: 'mail-api'
    repository_token = System.getenv('REPOSITORY_TOKEN') ?: 'null'

    /* Project Information */
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngineMail'
}

allprojects {
    group = 'io.github.mcengine'

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven {
            name = "papermc"
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = project.properties[project.name + '.version'] ?: 'undefined'

    tasks.named('jar').configure {
        enabled = false
    }

    tasks.named('shadowJar').configure {
        archiveClassifier = null
    }

    if (project.path == ":mcengine-mail-api") {
        apply plugin: 'maven-publish'

        publishing {
            repositories {
                maven {
                    name = 'GitHubPackages'
                    url = uri("https://maven.pkg.github.com/${project.organization_name}/${project.repository_name}")
                    credentials {
                        username = project.user_git_name
                        password = project.user_github_token ?: project.repository_token ?: project.organization_token
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    artifact(tasks.named('shadowJar').get())

                    pom {
                        name.set(project.jarName)
                        description.set("The ${project.jarName} API is designed for use as an API.")
                        url.set("https://github.com/${project.organization_name}/${project.repository_name}")

                        scm {
                            connection.set("scm:git:git://github.com/${project.organization_name}/${project.repository_name}.git")
                            developerConnection.set("scm:git:ssh://github.com:${project.organization_name}/${project.repository_name}.git")
                            url.set("https://github.com/${project.organization_name}/${project.repository_name}")
                        }

                        developers {
                            developer {
                                id.set('jetsadawijit')
                                name.set('Jetsada Wijit')
                                email.set('jetsadawijit@outlook.com')
                                url.set('https://jetsadawijit.github.io')
                            }
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(path: ":mcengine-mail-api", configuration: 'shadow')
}

shadowJar {
    version = version
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

build.dependsOn(shadowJar)
